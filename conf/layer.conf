# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "updatehub-freescale"
BBFILE_PATTERN_updatehub-freescale = "^${LAYERDIR}/"
BBFILE_PRIORITY_updatehub-freescale = "6"

LAYERDEPENDS_updatehub-freescale = "updatehub"


###
# Global setting to use UpadateHub with meta-freescale layer
#
# Setting to use wic image
IMAGE_BOOT_FILES_updatehub-imx ??= ""
do_image_wic[depends] += "wic-tools:do_build virtual/bootloader:do_deploy"
IMAGE_FSTYPES_updatehub-imx ??= "tar.xz wic.gz"
WKS_SEARCH_PATH_updatehub-imx ??= "${THISDIR}:${@':'.join('%s/wic' % p for p in '${BBPATH}'.split(':'))}:${@':'.join('%s/scripts/lib/wic/canned-wks' % l for l in '${BBPATH}:${COREBASE}'.split(':'))}"

# UpdateHub settings
UPDATEHUB_ACTIVE_INACTIVE_BACKEND_updatehub-imx ??= "u-boot"
UPDATEHUB_DEVICE_IDENTITY_updatehub-imx ??= "primary-iface"
UPDATEHUB_FILESYSTEM_SUPPORT_updatehub-imx ??= "ext4"
UPDATEHUB_IMAGE_TYPE_updatehub-imx ??= "active/inactive"
UPDATEHUB_INSTALL_MODE_updatehub-imx ??= "tarball"

PREFERRED_PROVIDER_virtual/bootloader_updatehub-imx ??= "u-boot-fslc"
PREFERRED_RPROVIDER_u-boot-fw-utils_updatehub-imx ??= "u-boot-fslc-fw-utils"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_updatehub-imx += " \
    kernel-image \
    kernel-devicetree \
"


###
# Configuration for i.MX6 SABRE AUTO and SABRE Smart Device
#
# UpdateHub settings for imx6dlsabresd machine
MACHINEOVERRIDES_prepend_imx6dlsabresd = "updatehub-imx:"
WKS_FILES_updatehub-imx_imx6dlsabresd ??= "updatehub.imx-uboot.wks"

# UpdateHub settings for imx6qsabresd machine
MACHINEOVERRIDES_prepend_imx6qsabresd = "updatehub-imx:"
WKS_FILES_updatehub-imx_imx6qsabresd ??= "updatehub.imx-uboot.wks"


###
# Configuration for i.MX6 Wandboard Quad/Dual/Solo
#
# UpdateHub settings for wandboard machine
MACHINEOVERRIDES_prepend_wandboard = "updatehub-imx:"
WKS_FILES_updatehub-imx_wandboard ??= "updatehub.imx-uboot-spl.wks"
